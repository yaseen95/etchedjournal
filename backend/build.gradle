buildscript {
    ext {
        kotlinVersion = '1.2.21'
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: "kotlin-jpa"
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.etchedjournal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
}


dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile "org.flywaydb:flyway-core:5.0.6"
    compile 'io.jsonwebtoken:jjwt:0.9.0'
    compile('commons-validator:commons-validator:1.6')

    // This is needed to convert java8 time objects e.g. Instant, LocalDateTime, etc.
    compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.4')

    // This enables jackson to instantiate kotlin classes that don't have a default constructor.
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4"

    // hibernate-core:5.0.12.Final has a bug mapping LocalDateTime to H2 timestamp. Have to use
    // other library. https://stackoverflow.com/questions/44676732/
    compile('org.hibernate:hibernate-java8:5.0.12.Final')

    runtime('org.springframework.boot:spring-boot-devtools')
    runtime group: 'com.h2database', name: 'h2', version: '1.4.196'
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
}

jar {
    // Copy the react bundle into the jar.
    from('../frontend/build') {
        into 'static'
    }
}

processResources.dependsOn(":frontend:bundle")
